








// #define VC_WF(x) (forall int _i :: 0 <= _i && _i < this.vc.length ==> (x[_i] / 256) >= 0 && (x[_i] % 256) == _i);


//#define VC_WF_ENTRY(x,i) ((x / 256) >= 0 && (x % 256) == i)


/*

 An Anchor implementation of the VerifiedFT-v2 algorithm from

  VerifiedFT: A Verified, High-Performance Precise Dynamic Race Detector
  James R. Wilcox, Cormac Flanagan, Stephen N. Freund
  PPOPP, 2018

 We refer the reader to that paper for a full discussion of the
 synchronization and algorithm.

 Notes:

  * readonly === isRead() ? B : E

  * In the original, inheritance was used to create three specialized
    versions of VectorClocks for ThreadState, VarState, LockState.  In
    Anchor, we needed to duplicate the VectorClock code in each of the
    three classes since there is no inheritance.
    
  * Unlike the original, we include initializers for each of the
    three main classes.

  * We use array abbreviations in Anchor, which were not included in
    the paper, eg:

    array T = int[moves_as ...]

    This instroduces an array type abbreviation that can be used
    later, as in

    [T] v = new [T](10);

  * In array specs, the array object is referred to as "athis", and the
    enclosing object is "this".

  * We encode an ecoch tid@c as (tid + c * 256).  That is, the lower 8
    bits of an int is the tid and the upper 24 is the clock.  We
    assume no more than 255 threads will be used and that overflow
    does not occur.  We use the constant -1 to represent the SHARED
    special value.  This would be better implemented with bit-wise
    operations, but Anchor doesn't support them.

*/

class Object { }

class VarState {

  array VC_VarState = int[moves_as
    isLocal(athis) ? B
                   : (this.read != -1)
                        ? guarded_by this
                        : isRead() ? (holds(this) || tid == index ? B:E)
                                   : (holds(this) && tid == index ? B:E)]

  array VC_Local = int[moves_as isRead() ? B : E]
  volatile int read
    moves_as isLocal(this) ? threadlocal
                           : (this.read != -1) ? write_guarded_by this
                                               : (isRead() ? R : E);

  volatile int write
    moves_as isLocal(this) ? threadlocal : write_guarded_by this;
    
  volatile [VC_VarState] vc 
    moves_as isLocal(this) ? threadlocal 
                           : (this.read != -1) ? guarded_by this : write_guarded_by this;

  invariant (forall int _i :: 0 <= _i && _i < this.vc.length ==> (this.vc[_i] >= _i));
  invariant this.vc.length <= 256;
  invariant this.write >= 0;
  invariant this.read >= -1;

  invariant isShared(this) ==> this.vc != null;

  public VarState() {
    this.read = 0;
    this.write = 0;
    this.vc = new [VC_VarState](0);
  }

  int get(int i) {
    [VC_VarState] vc = this.vc;
    assert i >= 0;
    int x = (i < vc.length) ? vc[i] : i;
    return x;
  }

  void ensureCapacity(int n) {
    if (n > this.vc.length) {
      [VC_Local] o = new [VC_Local](this.vc.length);  // spec
      assume (forall int k :: o[k] == this.vc[k]);    // spec

      [VC_VarState] newVC = new [VC_VarState](n);

      assert n <= 256;
      for (int i = 0; i < n; i = i + 1)
        invariant 0 <= i && i <= n;
        invariant (forall int _i :: 0 <= _i && _i < i && _i < n ==> newVC[_i] == (( _i < this.vc.length) ? this.vc[ _i] : ( _i)));   // spec
        invariant (forall int _i :: this.vc[_i] == o[_i]);   // spec
        invariant isLocal(newVC) && isFresh(newVC);          // spec
        invariant (forall int _i :: 0 <= _i && _i < i && _i < n ==> (newVC[_i] >=  _i));
      {
        newVC[i] = this.get(i);
        assert (newVC[i] >=  i);
      }
      this.vc = newVC;
    }
  }

  void set(int index, int value) {
    this.ensureCapacity(index + 1);
    this.vc[index] = value;
  }

  int size() {
    return this.vc.length;
  }

  bool leq(ThreadState st) {
    int stSize = st.size();
    int thisSize = this.size();
    int n = ((stSize) > ( thisSize) ? (stSize) : ( thisSize));
    int i = 0;
    assert n <= 256;
    while (i < n)
      invariant 0 <= i && i <= n;   // spec
      invariant (forall int _i :: 0 <= _i && _i < i && _i < n ==> ((( _i < this.vc.length) ? this.vc[ _i] : ( _i))) <= ( (( _i < st.vc.length) ? st.vc[ _i] : ( _i))));  // spec
    {
      if (!((this.get(i)) <= ( st.get(i)))) {
        return false;
      }
      assert ((( i < this.vc.length) ? this.vc[ i] : ( i))) <= ( (( i < st.vc.length) ? st.vc[ i] : ( i)));
      i = i + 1;
    }
    return true;
  }
}

class LockState {
  array VC_LockState = int[moves_as  guarded_by this.target]

  array VC_Local = int[moves_as isRead() ? B : E]
  
  Object target moves_as isLocal(this) ? threadlocal : readonly;

  [VC_LockState] vc moves_as isLocal(this) ? threadlocal : guarded_by this.target;

  invariant (forall int _i :: 0 <= _i && _i < this.vc.length ==> (this.vc[_i] >= _i));
  invariant this.vc.length <= 256;
  invariant isShared(this.target);
  invariant isShared(this) ==> this.vc != null;

  requires target != null;
  public LockState(Object target) {
    this.target = target;
    this.vc = new [VC_LockState](0);
  }

  int get(int i) {
    [VC_LockState] vc = this.vc;
    return (i < vc.length) ? vc[i] : i;
  }

  void ensureCapacity(int n) {
    [VC_LockState] vc = this.vc;

    if (n > vc.length) {
      [VC_LockState] newVC = new [VC_LockState](n);
      for (int i = 0; i < n; i = i + 1)
        invariant (forall int _i :: 0 <= _i && _i < i ==> newVC[_i] == (( _i < vc.length) ? vc[ _i] : ( _i)));   // spec
        invariant isLocal(newVC) && isFresh(newVC);    // spec
      {
        newVC[i] = this.get(i);
      }
      this.vc = newVC;
    }
  }

  void set(int index, int value) {
    this.ensureCapacity(index + 1);
    this.vc[index] = value;
  }

  int size() {
    return this.vc.length;
  }

  void copy(ThreadState st) {
    [VC_LockState] oldVCPtr = this.vc;
    
    [VC_Local] o = new [VC_Local](st.vc.length);  // spec
    assume (forall int k :: o[k] == st.vc[k]);   // spec

    this.ensureCapacity(st.size());
    for (int j = 0; j < this.size(); j = j + 1)
      invariant 0 <= j && j <= this.vc.length;    // spec
      invariant (forall int i :: 0 <= i && i < j && i < this.vc.length ==> (this.vc[i] == (( i < st.vc.length) ? st.vc[ i] : ( i))));  // spec
      invariant (forall int i :: st.vc[i] == o[i]);  // spec
      invariant this.vc == oldVCPtr || isFresh(this.vc);  // spec
    {
      int x = st.get(j);
      this.vc[j] = x;
    }
  }
}

class ThreadState {

  array VC_ThreadState = int[moves_as
      isLocal(this) ? threadlocal
                    : this.stopped ? (isRead() ? B : E)
                                   : (tid == this.t ? B:E)]

  array VC_Local = int[moves_as isRead() ? B : E]

  volatile bool stopped moves_as
    isLocal(this) ? threadlocal :
      isRead() ? (tid == this.t ? B : (this.stopped ? R : N))
               : (tid == this.t && !this.stopped && newValue ? N : E);

  Tid t     moves_as  isLocal(this) ? threadlocal : readonly;

  invariant 0 <= this.t && this.t < 256;

  volatile [VC_ThreadState] vc  moves_as
      isLocal(this) ? threadlocal
                    : this.stopped ? isRead() ? B : E
                                   : (tid == this.t ? B:E);

  invariant this.vc.length <= 256;
  invariant (forall int _i :: 0 <= _i && _i < this.vc.length ==> (this.vc[_i] >= _i));

  invariant isShared(this.vc) && this.vc != null;
  invariant this.vc.length > this.t;


  int get(int i) {
    [VC_ThreadState{this}] vc = this.vc;
    return (i < vc.length) ? vc[i] : i;
  }

  void set(int index, int value) {
    this.ensureCapacity(index + 1);
    this.vc[index] = value;
  }

  void ensureCapacity(int n) {
        [VC_ThreadState] vc = this.vc;

    if (n > vc.length) {
      [VC_ThreadState] newVC = new [VC_ThreadState](n);
      [VC_Local] o = new [VC_Local](vc.length);  // spec
      assume (forall int k :: o[k] == vc[k]); // spec

      for (int i = 0; i < n; i = i + 1)
        invariant (forall int _i :: 0 <= _i && _i < i && _i < n ==> newVC[_i] == (( _i < vc.length) ? vc[ _i] : ( _i)));  // spec
        invariant (forall int _i :: 0 <= _i && _i < vc.length ==> vc[_i] == o[_i]);   // spec
        invariant isFresh(newVC) && isLocal(newVC);  // spec
	    invariant (forall int k :: o[k] == vc[k]); // spec
        invariant (forall int _i :: 0 <= _i && _i < i && _i < n ==> (newVC[_i] >=  _i));
      {
        newVC[i] = this.get(i);
        assert (newVC[i] >=  i);
        assert newVC[i] == (( i < vc.length) ? vc[ i] : ( i));
      }
      this.vc = newVC;
    }
  }

  int size() {
    return this.vc.length;
  }

  void joinThread(ThreadState st) {
    for (int i = 0; i < st.size(); i = i + 1) {
      this.set(i, ((this.get(i)) > ( st.get(i)) ? (this.get(i)) : ( st.get(i))));
    }
  }

  void inc(int t) {
    this.set(t, (this.get(t) + 256));
  }


  requires tid == this.t;  
  requires !this.stopped;
  {   
	modifies $result;   // spec
	ensures (old(sx.write == (( tid < this.vc.length) ? this.vc[ tid] : ( tid)) && old(sx.vc) == sx.vc)) ==> $result;   // spec
  } yield; {
    modifies sx;   // spec

    ensures old(sx.read) == sx.read;  // spec
    ensures old(sx.vc) == sx.vc;      // spec

    ensures (old(sx.write <= (( (sx.write % 256) < this.vc.length) ? this.vc[ (sx.write % 256)] : ( (sx.write % 256)))    // spec
               && (sx.read != -1 ==> sx.read <= (( (sx.read % 256) < this.vc.length) ? this.vc[ (sx.read % 256)] : ( (sx.read % 256))))  // spec
               && (sx.read == -1 ==> (forall int _i :: 0 <= _i && _i < ((this.vc.length) > ( sx.vc.length) ? (this.vc.length) : ( sx.vc.length)) ==> (( _i < sx.vc.length) ? sx.vc[ _i] : ( _i)) <= (( _i < this.vc.length) ? this.vc[ _i] : ( _i)))))) == $result;  // spec

//    ensures (old(sx.write <= (( (sx.write % 256) < this.vc.length) ? this.vc[ (sx.write % 256)] : ( (sx.write % 256))))) == $result;    // spec

    ensures $result  ==> sx.write == old((( tid < this.vc.length) ? this.vc[ tid] : ( tid)));     // spec
    ensures !$result ==> sx.write == old(sx.write);                 // spec

  }
  public bool write(VarState sx) {
    int e = this.get(this.t);
    int w = sx.write;
    if (w == e) { return true; }
    yield;
    assume !nextSpecStep(0);  // spec
    synchronized(sx) {
      w = sx.write;
      int ww = this.get((w % 256));
      if (w > ww) {
//        assert !(sx.write <= (( (sx.write % 256) < this.vc.length) ? this.vc[ (sx.write % 256)] : ( (sx.write % 256))));
        return false;
      }
      int r = sx.read;
      if (r != -1) {
        int rr = this.get((r % 256));
        if (r > rr) {
//          assert !(sx.read != -1 ==> sx.read <= (( (sx.read % 256) < this.vc.length) ? this.vc[ (sx.read % 256)] : ( (sx.read % 256))));
          return false;
        }
      } else {
        if (!sx.leq(this)) {
           return false;
        }
      }
//      assert sx.write <= (( (sx.write % 256) < this.vc.length) ? this.vc[ (sx.write % 256)] : ( (sx.write % 256)));
//      assert sx.read != -1 ==> sx.read <= (( (sx.read % 256) < this.vc.length) ? this.vc[ (sx.read % 256)] : ( (sx.read % 256)));
      sx.write = e;
      return true;
    }
  }

  requires tid == this.t;
  requires !this. stopped;
  {
    modifies $result;   // spec
    ensures (old(sx.read == (( tid < this.vc.length) ? this.vc[ tid] : ( tid)) || sx.read == -1 && ((( tid < this.vc.length) ? this.vc[ tid] : ( tid))) == (( tid < sx.vc.length) ? sx.vc[ tid] : ( tid)))) ==> $result;    // spec
  } yield; {
    modifies sx, sx.vc;   // spec

    ensures old(sx.write) == sx.write;  // spec

    ensures (old(sx.write <= (( (sx.write % 256) < this.vc.length) ? this.vc[ (sx.write % 256)] : ( (sx.write % 256))))) == $result;  // spec

    ensures !$result ==> (old(sx.read) == sx.read);   // spec
    ensures !$result ==> (forall int i :: old(sx.vc) == sx.vc);   // spec
    ensures !$result ==> (forall int i :: old(sx.vc[i]) == sx.vc[i]);   // spec
    ensures $result ==>  (sx.read == old((( tid < this.vc.length) ? this.vc[ tid] : ( tid)))    // spec
                     || (old(sx.read) != -1 && sx.read == -1 && (forall int _i :: 0 <= _i && _i < sx.vc.length ==> sx.vc[_i] == old(_i == tid ? this.vc[tid]  // spec
  		       		     	      	      	       	       	      	                                                             : (_i == (sx.read % 256) ? sx.read // spec
                                                                       									     : (((_i < sx.vc.length) ? sx.vc[_i] : (_i)))))))  // spec
  		    || (old(sx.read) == -1 && sx.read == -1 && (forall int _i :: 0 <= _i && _i < sx.vc.length ==> sx.vc[_i] == old(_i == tid ? this.vc[tid]  // spec
  		       		     	      	      	       	     	                                                               : ((_i < sx.vc.length) ? sx.vc[_i] : (_i))))));   // spec
  }
  public bool read(VarState sx) {
    int t = this.t;
    int e = this.get(t);

    int r;
    r = sx.read;
    if (r == e || r == -1 && sx.get(t) == e) { return true; }
    yield;
    assume !nextSpecStep(0);   // spec
    synchronized(sx) {
      int w = sx.write;
      int ww = this.get((w % 256));
      if (w > ww) { return false; }
      r = sx.read;
      if (r != -1) {
        int rr = this.get((r % 256));
        if (r <= rr) {
          sx.read = e;
        } else {
          sx.set((r % 256), r);
          sx.set(t, e);
          sx.read = -1;
          return true;
        }
      } else {
        sx.set(t, e);
      }
    }
    return true;
  }


  requires tid == this.t;
  requires !this.stopped;
  requires holds(sm.target);
  modifies this, this.vc;   // spec
  ensures this.stopped == old(this.stopped);   // spec
  ensures this.vc.length >= sm.vc.length;   // spec
  ensures (forall int i :: 0 <= i && i < sm.vc.length ==> this.vc[i] == old((((( i < sm.vc.length) ? sm.vc[ i] : ( i))) > ( (( i < this.vc.length) ? this.vc[ i] : ( i))) ? ((( i < sm.vc.length) ? sm.vc[ i] : ( i))) : ( (( i < this.vc.length) ? this.vc[ i] : ( i))))));   // spec
  ensures (forall int i :: sm.vc.length <= i && i < this.vc.length ==> this.vc[i] == old(this.vc[i]));    // spec
  public void acq(LockState sm) {
    [VC_ThreadState] oldThisVc = this.vc;
    [VC_Local] oldVc = new [VC_Local](this.vc.length);  // spec
    assume (forall int k :: oldVc[k] == this.vc[k]);  // spec
    for (int j = 0; j < sm.size(); j = j + 1) 
      invariant j <= sm.vc.length;   // spec
      invariant this.vc.length >= j;  // spec
      invariant (forall int i :: 0 <= i && i < j ==> this.vc[i] == (((( i < sm.vc.length) ? sm.vc[ i] : ( i))) > ( (( i < oldVc.length) ? oldVc[ i] : ( i))) ? ((( i < sm.vc.length) ? sm.vc[ i] : ( i))) : ( (( i < oldVc.length) ? oldVc[ i] : ( i)))));  // spec
      invariant (forall int i :: j <= i && i < this.vc.length ==> this.vc[i] == oldVc[i]);  // spec
      invariant this.vc == oldThisVc || (isFresh(this.vc));  // spec
    {
      assert oldVc[j] == this.vc[j]; 
      this.set(j, ((this.get(j)) > ( sm.get(j)) ? (this.get(j)) : ( sm.get(j))));
    }
  }


  requires tid ==this.t;
  requires !this.stopped;
  requires holds(sm.target);
  modifies sm, sm.vc;
  ensures (forall int i :: 0 <= i && i < sm.vc.length ==> sm.vc[i] == (( i < this.vc.length) ? this.vc[ i] : ( i)));  // spec
  public void rel(LockState sm) {
    sm.copy(this);
  }



  requires (t == 0);  // only checked for client-side call, not nested call in fork...
  ensures !this.stopped;   // spec
  ensures this.t == t;    // spec
  ensures this.vc.length == 1;    // spec
  ensures this.vc[0] == 256;     // spec
  public ThreadState(int t, ThreadState forker) {
    this.t = t;
    this.stopped = false;

    if (t != 0) {

      [VC_Local]oldVc = new[VC_Local] (forker.vc.length);  // spec
      assume (forall int k :: oldVc[k] == forker.vc[k]);  // spec
      
      int n = ((t + 1) > ( forker.size()) ? (t + 1) : ( forker.size()));
      [VC_ThreadState] vc = new[VC_ThreadState] (n);
      for (int i = 0; i < n; i = i + 1) 
        invariant 0 <= i && i <= n;   // spec
        invariant(forall int _i :: 0 <= _i && _i < i && _i < n ==> vc[_i] == (_i == t ? ((( _i < forker.vc.length) ? forker.vc[ _i] : ( _i)) + 256) : (( _i < forker.vc.length) ? forker.vc[ _i] : ( _i))));   // spec
        invariant isLocal(vc) && isFresh(vc);    // spec
        invariant(forall int _i :: forker.vc[_i] == oldVc[_i]);  // spec
        invariant n <= vc.length;   // spec
        invariant(forall int _i :: 0 <= _i && _i < i && _i < n ==>(vc[_i] >=  _i));  // for performance...   // spec
      {
        vc[i] = i == t ? (forker.get(i) + 256) : forker.get(i);
        assert (vc[i] >=  i);
      }
      this.vc = vc;
    } else {
      [VC_ThreadState] vc = new[VC_ThreadState](1);
      vc[0] = 256;
      this.vc = vc;
    }
  }


  requires tid == this.t;
  requires !this.stopped;
  requires this.t != uTid;
  requires uTid > 0 && uTid < 256;
  modifies this.vc;   // spec
  ensures (forall int i :: 0 <= i && i < this.vc.length ==> this.vc[i] == old(i != tid ? this.vc[i] : (this.vc[tid] + 256)));   // spec
  ensures !$result.stopped;   // spec
  ensures $result.t == uTid;   // spec
  ensures (forall int i :: 0 <= i && i < $result.vc.length ==> $result.vc[i] == old(i != uTid ? ((i < this.vc.length) ? this.vc[i] : (i)) : (((i < this.vc.length) ? this.vc[i] : (i)) + 256)));   // spec
  public ThreadState fork(int uTid) {
    // We create the new ThreadState object here so that we
    // have threadlocal access to it.
    ThreadState u = new ThreadState(uTid, this);
    this.inc(this.t);
    return u;
  }


  requires tid == this.t;
  requires tid != u.t;
  requires !this.stopped;
  requires u.stopped;
  modifies this;

  ensures this.vc[tid] == old(((((( tid < u.vc.length) ? u.vc[ tid] : ( tid))) > ( (( tid < this.vc.length) ? this.vc[ tid] : ( tid))) ? ((( tid < u.vc.length) ? u.vc[ tid] : ( tid))) : ( (( tid < this.vc.length) ? this.vc[ tid] : ( tid)))) + 256));  // spec
  ensures (forall int _i :: 0 <= _i < this.vc.length && _i != tid ==> this.vc[_i] == (((( _i < u.vc.length) ? u.vc[ _i] : ( _i))) > ( old((( _i < this.vc.length) ? this.vc[ _i] : ( _i)))) ? ((( _i < u.vc.length) ? u.vc[ _i] : ( _i))) : ( old((( _i < this.vc.length) ? this.vc[ _i] : ( _i))))));  // spec


  public void join(ThreadState u) {
    [VC_ThreadState] oldVCPtr = this.vc;
    int n = ((u.size()) > ( this.size()) ? (u.size()) : ( this.size()));
    [VC_Local] orig = new [VC_Local](this.vc.length);  // spec
    assume (forall int k :: orig[k] == this.vc[k]); // spec

    for (int i = 0; i < n; i = i + 1)
      invariant i <= this.vc.length <= n;
      invariant (forall int _i :: i <= _i < n ==> (( _i < this.vc.length) ? this.vc[ _i] : ( _i)) == (( _i < orig.length) ? orig[ _i] : ( _i)));
      invariant (forall int _i :: 0 <= _i && _i < i ==> this.vc[_i] == (((( _i < u.vc.length) ? u.vc[ _i] : ( _i))) > ( (( _i < orig.length) ? orig[ _i] : ( _i))) ? ((( _i < u.vc.length) ? u.vc[ _i] : ( _i))) : ( (( _i < orig.length) ? orig[ _i] : ( _i)))));
      invariant this.vc == oldVCPtr || (isFresh(this.vc));     // spec
    {
      int x = ((u.get(i)) > ( this.get(i)) ? (u.get(i)) : ( this.get(i)));
      assert x == (((( i < u.vc.length) ? u.vc[ i] : ( i))) > ( (( i < orig.length) ? orig[ i] : ( i))) ? ((( i < u.vc.length) ? u.vc[ i] : ( i))) : ( (( i < orig.length) ? orig[ i] : ( i))));
      this.set(i, x);
      assert this.vc[i] == (((( i < u.vc.length) ? u.vc[ i] : ( i))) > ( (( i < orig.length) ? orig[ i] : ( i))) ? ((( i < u.vc.length) ? u.vc[ i] : ( i))) : ( (( i < orig.length) ? orig[ i] : ( i))));
    }
    this.inc(this.t);
  }
}

