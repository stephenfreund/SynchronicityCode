
class Node {
    int item  moves_as isLocal(this) ? threadlocal : readonly;
    volatile Node next moves_as isLocal(this) ? threadlocal : write_guarded_by this;

    invariant -5 <= this.item && this.item <= 5;
    invariant this.next != null ==> this.item < this.next.item;
    invariant this.next == null ==> this.item == 5;
}

class OptimisticList {

    Node head moves_as isLocal(this) ? threadlocal : readonly;
    invariant this.head != null && this.head.item == -5;

    bool validate(Node pred, Node curr, int item) {
        Node node = this.head;
        yield;
        while (node.item <= pred.item)
          invariant isShared(node);
          invariant isShared(curr);     // UGH
          invariant isShared(pred);     // UGH
          invariant holds(curr);        // UGH
          invariant holds(pred);        // UGH
          invariant pred.item < item;   // UGH
          invariant curr.item >= item;  // UGH
        {
          if (node == pred) {
            return pred.next == curr;
          }
          node = node.next;
          yield;
        }
        return false;
    }

    requires -5 < item && item < 5;
    public bool add(int item) {
      while (true) {
        Node pred = this.head;
        Node curr = pred.next;
        yield;
        while (curr.item < item)
          invariant isShared(curr);
          invariant isShared(pred);
          invariant pred.item < item;
        {
          pred = curr;
          yield;
          curr = curr.next;
          yield;
        }
         assert pred.item < item;
        yield;
         assert pred.item < item;
        acquire(pred);
        acquire(curr);
         assert pred.item < item;
         assert curr.item >= item;

       if (this.validate(pred, curr, item)) {
         assert pred.next == curr;
         assert pred.item < item;
         assert curr.item >= item;
         if (curr.item == item) {
           release(curr);
           release(pred);
           return false;
         } else {
           Node node2 = new Node();
           node2.item = item;
           node2.next = curr;
           pred.next = node2;
           release(curr);
           release(pred);
           return true;
         }
       }
      }
    }


     requires -5 < item && item < 5;
     public bool contains(int item) {
       while (true) {
         Node pred = this.head;
         Node curr = pred.next;
         yield;
         while (curr.item < item)
           invariant isShared(curr);
           invariant isShared(pred);
           invariant pred.item < item;
         {
           pred = curr;
           yield;
           curr = curr.next;
           yield;
         }
         acquire(pred);
         acquire(curr);
         if (this.validate(pred, curr, item)) {
           bool b = (curr.item == item);
           release(curr);
           release(pred);
           return b;
         }
       }
     }
}
