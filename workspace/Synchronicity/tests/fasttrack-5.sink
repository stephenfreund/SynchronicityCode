class VarState {

    array VarState_VC = int[
        isLocal(athis) ? B
                           : (this.read != -1)
                               ? guarded_by this
                               : (((holds(this) || tid == index) ? B:E) # ((holds(this) && tid == index) ? B:E))
    ]


    int read  (this.read != -1) ? (holds(this) ? B#N : N#E)
                                : R#E;

    int write  write_guarded_by this;
    [VarState_VC] readVC (this.read != -1) ? guarded_by this : write_guarded_by this;

    invariant isShared(this) ==> this.readVC != [VarState_VC].null;

    int get(int i) {
        [VarState_VC] readVC := this.readVC;
        if (i < readVC.length) {
            int t := readVC[i];
            return t;
        } else {
            return 0;
         }
     }

    void ensureCapacity(int n) {
        [VarState_VC] readVC := this.readVC;

        if (n > readVC.length) {
            [VarState_VC] newVC = new [VarState_VC](n);
            int i = 0;
            while (i < n) {
                int temp := readVC[i];
                newVC[i] := temp;
                i = i + 1;
            }
            this.readVC := newVC;
         }
    }

    void set(int index, int value) {
        this.ensureCapacity(index + 1);
        [VarState_VC] readVC := this.readVC;
        readVC[index] := value;
     }

    int size() {
        [VarState_VC] readVC := this.readVC;
        int t = readVC.length;
        return t;
     }

     int max(int v1, int v2) {
         if (v1 < v2) {
             return v2;
         } else {
             return v1;
         }
      }
      int TID(int e) {
          int t = e % 256;
          return t;
      }

      bool LEQ(int e1, int e2) {
          int v1 = this.TID(e1);
          int v2 = this.TID(e2);
          bool t = (v1 == v2 && e1 < e2);
          return t;
      }


      bool leq(ThreadState st) {
        int l1 = this.size();
        int l2 = st.size();
        int n = this.max(l1, l2);
        int i = 0;
        while (i < n) {
            int v1 = this.get(i);
            int v2 = st.get(i);
            bool l = this.LEQ(v1, v2);
            if (!l) {
                return false;
            }
            i = i + 1;
        }
        return true;
      }

}

class ThreadState {

    array ThreadState_VC = int[tid == this.t ? B:E ]

    Tid t                   readonly;
    [ThreadState_VC] stV    tid == this.t ? B:E;

    invariant (forall int _i :: this.stV[_i] >= 0);


    int TID(int e) {
        int t = e % 256;
        return t;
    }
    bool LEQ(int e1, int e2) {
        int v1 = this.TID(e1);
        int v2 = this.TID(e2);
        bool t = (v1 == v2 && e1 < e2);
        return t;
    }

    int get(int i) {
        [ThreadState_VC] stV := this.stV;
        if (i < stV.length) {
            int t := stV[i];
            return t;
        } else {
            return 0;
         }
     }

     int size() {
        [ThreadState_VC] stV := this.stV;
        int t = stV.length;
        return t;
      }


    requires tid == this.t;
    public bool write(VarState sx) {

        int t := this.t;
        int e = this.get(t);
        int w := sx.write;
        if (w == e) { return true; }
        yield;
        synchronized(sx) {
            int ww = this.get(w);
            if (w > ww) { return false; }
            int r := sx.read;
            if (r != -1) {
                int rr = this.get(r);
                if (r > rr) { return false; }
                sx.write := e;
            } else {
                bool isLess = sx.leq(this);
                if (!isLess) {
                   return false;
                }
                sx.write := e;
            }
        }
        return true;
    }

    requires tid == this.t;
    public bool read(VarState sx) {
        int t := this.t;
        int e = this.get(t);

        int r := sx.read;
        if (r == e) {
            return true;
        } else {
            if (r == -1) {
                //assert sx.read == -1;  // sanity check...
                int readVCt = sx.get(t);
                if (readVCt == e) { return true; }
            }
        }
        yield;

        synchronized(sx) {
            int w := sx.write;
            int ww = this.get(w);
            if (w > ww) { return false; }
            r := sx.read;
            if (r != -1) {
                int rr = this.get(r);
                if (r > rr) {
                    sx.read := e;
                } else {
                    sx.set(r, r);
                    sx.set(t, e);
                    sx.read := -1;
                }
            } else {
                    sx.set(t, e);
            }
        }
        return true;
    }

}
